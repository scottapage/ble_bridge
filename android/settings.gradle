import java.util.Properties

pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }

    def props = new Properties()
    def localProps = new File(rootDir, "local.properties")
    def flutterSdkPath
    if (localProps.exists()) {
        localProps.withReader("UTF-8") { props.load(it) }
        flutterSdkPath = props.getProperty("flutter.sdk")
    } else {
        flutterSdkPath = System.getenv("FLUTTER_ROOT")
    }
    if (flutterSdkPath == null) {
        throw new GradleException("Flutter SDK not found. Set FLUTTER_ROOT or define flutter.sdk in android/local.properties")
    }
    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.6.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.24" apply false
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        google()
        mavenCentral()
        maven { url "https://storage.googleapis.com/download.flutter.io" }
    }
}

rootProject.name = "ble_bridge"
include(":app")
